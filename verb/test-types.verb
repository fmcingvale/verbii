\ Testing of types, other than what is done in e.g. test-string.verb
\
\ Copyright (c) 2022 Frank McIngvale, see LICENSE

: test-type-comparisons
	\ NOTE -- when testing '==', need to swap args as well when types are different.
	\ i.e. 'int == string' might be checked but 'string == int' could have been missed
	\ in implementing ==
	
	\ there is some overlap between this and other tests like test-numeric and test-string, but
	\ this is specifically for the two-sided test, not an exhastive test of the == for any specific
	\ type, as is done in other test modules
	
	\ for each type, test == then != on both sides
	
	\ int 
	123 123 == expect-true
	123 "123" == expect-false
	"123" 123 == expect-false
	
	\ float
	123.25 123.25 == expect-true
	123.25 "123.25" == expect-false
	"123.25" 123.25 == expect-false
	
	\ string
	"abcd" "abcd" == expect-true
	"abcd" 'abcd == expect-false
	'abcd "abcd" == expect-false
	
	\ symbol
	'abcd 'abcd == expect-true
	'abcd "abcd" == expect-false
	"abcd" 'abcd == expect-false
	
	\ bool
	true true == expect-true
	true "true" == expect-false
	"true" true == expect-false
	
	\ null
	null null == expect-true
	null void == expect-false
	void null == expect-false
	
	\ list
	[ 1 2 3 ] [ 1 2 3 ] == expect-true
	[ 1 2 3 ] "[ 1 2 3 ]" == expect-false
	"[ 1 2 3 ]" [ 1 2 3 ] == expect-false
	
	\ void
	void void == expect-true
	void 'void == expect-false
	'void void == expect-false
	
	\ dict
	[ [ "one" 1 ] [ "two" 2 ] ] make-dict
	[ [ "one" 1 ] [ "two" 2 ] ] make-dict == expect-true
	[ [ "one" 1 ] [ "two" 2 ] ] make-dict [ [ "one" 1 ] [ "two" 2 ] ] == expect-false
	[ [ "one" 1 ] [ "two" 2 ] ] [ [ "one" 1 ] [ "two" 2 ] ] make-dict == expect-false
	
	
	\ lambdas are never equal to anything, but it should not cause an error to compare them
	\ -> why? compiler does transformations/optimizations so user code should not rely on the
	\     source form of a lambda being equal to the compiled version so I think it's better to
	\	 disallow this comparison
	{ 1 2 3 } { 1 2 3 } == expect-false
	{ 10 20 30 } dup == expect-false
	{ 11 22 33 } 112233 == expect-false
	112233 { 11 22 33 } == expect-false
	
	\ same for bound lambdas
	{ 1 2 3 } bind-lambda dup == expect-false
	{ 1 2 3 } bind-lambda { 1 2 3 } bind-lambda == expect-false
	{ 1 2 3 } bind-lambda { 1 2 3 } == expect-false
	{ 1 2 3 } { 1 2 3 } bind-lambda == expect-false
	
	\ opcodes (have to use make-opcode, not literal notation, since that would run them)
	'FRAME-GET 1 2 3 make-opcode 'FRAME-GET 1 2 3 make-opcode == expect-true
	'FRAME-GET 1 2 3 make-opcode [ 1 2 3 ] == expect-false
	[ 1 2 3 ] 'FRAME-GET 1 2 3 make-opcode == expect-false
	
	\ now a few more random things ...
	
	\ bool correctness
	1 1 == 2 2 == == expect-true
	1 2 == 2 3 == == expect-true
	1 1 == 2 1 == == expect-false
	2 1 == 1 1 == == expect-false

	\ should not be any bool coercion
	0 false == expect-false
	1 true == expect-false 
	"abc" true == expect-false
	"" false == expect-false
	null false == expect-false
	void false == expect-false
	
	\ void is the only value equal to void
	2 void == expect-false
	void 2 == expect-false
	void void == expect-true
	
	'FRAME-GET 1 2 3 make-opcode 123 == expect-false
	123 'FRAME-GET 1 2 3 make-opcode == expect-false
	;
	
: test-types-testing
	123 int? expect-true
	#123 int? expect-false
	#446 float? expect-true
	123 float? expect-false

	"hello" string? expect-true
	"there" symbol? expect-false
	'world symbol? expect-true
	'foobar string? expect-false
	
	true bool? expect-true
	false bool? expect-true
	1 bool? expect-false

	null null? expect-true
	0 null? expect-false
	null void? expect-false
	
	void void? expect-true
	void null? expect-false
	0 void? expect-false
	
	{ 1 2 3 } lambda? expect-true
	{ 1 2 3 } bind-lambda lambda? expect-false
	{ 1 2 3 } bound-lambda? expect-false
	{ 1 2 3 } bind-lambda bound-lambda? expect-true

	'FRAME-GET 11 22 33 make-opcode opcode? expect-true
	123 opcode? expect-false
	;
	
: test-repr
	\ test that repr makes expected strings for all types
	"1234" 1234 repr expect==
	"#3.75" #3.75 repr expect==
	"#4.125" 4.125 repr expect==
	"<true>" true repr expect==
	"<false>" false repr expect==
	"[ 10 20 'hello #11.5 ]" 10 20 'hello 11.5 4 make-list repr expect==
	"{ 1 2 3 'a 'b 'c }" { 1 2 3 a b c } repr expect==
	"<bound { 1 2 3 'x 'y 'z }>" { 1 2 3 x y z } bind-lambda repr expect==
	"'a-long-symbol" 'a-long-symbol repr expect==
	""""hello there world"""" "hello there world" repr expect== \ """ <- so notepad++ sees end of string
	"<null>" null repr expect==
	"<*void*>" void repr expect==
	"#op( FRAME-GET 11 22 33 )" 'FRAME-GET 11 22 33 make-opcode repr expect==
	"#op( FRAME-SET 44 55 66 )" 'FRAME-SET 44 55 66 make-opcode repr expect==
	;
	
: test-str
	\ test that str makes expected strings for all types
	"1234" 1234 str expect==
	"3.75" #3.75 str expect==
	"4.125" 4.125 str expect==
	"true" true str expect==
	"false" false str expect==
	"[ 10 20 hello 11.5 ]" 10 20 'hello 11.5 4 make-list str expect==
	"{ 1 2 3 a b c }" { 1 2 3 a b c } str expect==
	"<bound { 1 2 3 x y z }>" { 1 2 3 x y z } bind-lambda str expect==
	"a-long-symbol" 'a-long-symbol str expect==
	"""hello there world""" "hello there world" str expect== 
	"<null>" null str expect==
	"<*void*>" void str expect==
	"#op( FRAME-GET 11 22 33 )" 'FRAME-GET 11 22 33 make-opcode str expect==
	"#op( FRAME-SET 44 55 66 )" 'FRAME-SET 44 55 66 make-opcode str expect==
	;