\ Simple chess engine - purely for fun, not practicality :-) Very simple & slow design.
\ Also this is a semi-real app so makes a better excercise for programming in verbii
\ versus the shorter demos.
\
\ Copyright (c) 2022 Frank McIngvale, see LICENSE
	
\ chess code starts here
	
\ pieces
: P-PAWN 0 ;
: P-KNIGHT 1 ;
: P-BISHOP 2 ;
: P-ROOK 3 ;
: P-QUEEN 4 ;
: P-KING 5 ;
: P-NONE 6 ;

'PIECE-CHARS [ "P" "N" "B" "R" "Q" "K" "." ] make-var

\ sides
: WHITE 0 ;
: BLACK 1 ;
( side -- other )
: other 1 + 2 mod ;

\ ------ board state -------

\ coordinates: row=0,col=0 is a1; row=0,col=7 is h1; row=7,col=7 is h8

\ pieces list (P-*), 1 for each square
'PIECES null make-var
	
\ colors list (WHITE|BLACK), 1 for each square
'COLORS null make-var

\ side to move next - WHITE or BLACK
'SIDE-TO-MOVE WHITE make-var

\ castling availability (bool) as W-OO, W-OOO, B-OO, B-OOO
'CASTLING null make-var

\ en-passant target square [0-63] or -1
'EPSQUARE -1 make-var

\ halfmove number for 50-move rule
'HALFMOVE 0 make-var

\ fullmove number
'FULLMOVE 0 make-var

\ must call this at least once to init board state
: clear-board ( -- ) 
	[ ] PIECES!
	{ PIECES P-NONE append drop } 0 1 64 for-range
	[ ] COLORS!
	{ COLORS WHITE append drop } 0 1 64 for-range
	WHITE SIDE-TO-MOVE!
	[ true true true true ] CASTLING!
	-1 EPSQUARE!
	;
	
\ get square (0..63) given row,col
\ ( row col -- square )
: square-index
	swap 8 * + 
	;
	
\ get row,col from square index
\ ( square -- row col )
: square-to-rowcol 
	8 /mod swap
	;
	
\ get piece at square
: piece-at ( square )
	PIECES square get
	;
	
\ get color at square (only valid if piece != P-NONE)
: color-at ( square )
	COLORS square get
	;
	
\ get printable char for given piece & color
: piece-char ( piece color -- char )
	color WHITE == if >>white
	PIECE-CHARS piece get tolower return \ black = lowercase
	@white PIECE-CHARS piece get
	;
	
\ get printable char for piece at square
: piece-char-at ( square -- char )
	PIECES square get
	COLORS square get
		piece-char
	;

\ parse a piece char into its P-* and color	
: char-to-piece ( char -- piece color )
	@locals [ ip ]
	PIECE-CHARS ::{ char toupper == } find ip! \ find index into PIECE_CHARS
	ip void? if >>bad-char
	char upper? if >>white
	ip BLACK return 
	@white ip WHITE return
	@bad-char "Bad piece character:" char str + error
	;

\ convert square (0..63) to algebraic notation
: square-to-algebraic ( square )
	square 8 /mod ( remainder quot )
	swap "a" ord + chr
	swap "1" ord + chr +
	;

\ convert algebraic notation (a1..h8) to square (0..63)
: algebraic-to-square ( alg )
	alg string? not if >>bad-alg
	alg length 2 != if >>bad-alg
	"abcdefgh" ::{ alg 0 get == } find void? if >>bad-alg
	"12345678" ::{ alg 1 get == } find void? if >>bad-alg
	alg 1 get ord "1" ord - 8 * 
	alg 0 get ord "a" ord - +
	return

	@bad-alg
		"Bad algebraic coordinate:" alg str + error
	;

: .. str puts ;

: print-board ( -- )
	@locals [ row col ]
	0 row!
	@loop-row
		row 8 >= if >>done-board
		0 col!
		@loop-col
			col 8 >= if >>done-row
			7 row - col square-index piece-char-at .
			col 1 + col! <<loop-col
		@done-row
			CR
			row 1 + row! <<loop-row
	@done-board

	\ side to move
	"Side to move:" .
	SIDE-TO-MOVE WHITE == 
		{ "white" . CR } { "black" . CR } ifelse

	\ castling status
	"Castling:" .
	CASTLING 0 get { "K" puts } ifthen
	CASTLING 1 get { "Q" puts } ifthen
	CASTLING 2 get { "k" puts } ifthen
	CASTLING 3 get { "q" puts } ifthen
	\ if no castling available, print - 
	CASTLING { } any? not { "-" . } ifthen
	CR
	
	"Ep-Target:" . 
	EPSQUARE -1 ==
		{ "None" . }
		{ EPSQUARE square-to-algebraic . }
		ifelse CR	
	
	"Halfmove:" . HALFMOVE .. ", Fullmove:" . FULLMOVE . CR
	;
	
: put-piece ( square piece color -- )
	PIECES square piece put 
	COLORS square color put 
	;

: square-empty? ( square -- result )
	PIECES square get P-NONE ==
	;
	
\ put & drop result, since normally putting into an object that
\ code has saved a ref to
: put- put drop ;

\ set board state from a FEN string
: set-from-fen ( fen -- )
	@locals [ parts row col c i ]
	clear-board
	\ split string into its fields
	fen " " string-split parts!
		\ "FEN PARTS:" . parts . CR
		
	\ parse pieces, e.g. "rnbqkbnr/pppppppp/8/8/8/8/PPPPPPPP/RNBQKBNR"
	7 row!
	0 col!
	0 i!
	@pieces-loop
		parts 0 get i get c! \ next char of string
			\ "ROW=" . row . "COL=" . col . "CHAR:" . c . CR
		c void? if >>pieces-end-string 
		c digit? if >>pieces-blanks \ got # for blank squares
		c "/" == if >>pieces-slash
		\ else it must be a piece for (row,col)
		row col square-index c char-to-piece 
			\ "CHAR TO PIECE:" . dup . CR 
			put-piece
		col 1 + col! >>pieces-next
	@pieces-blanks
		c parse-int col + col! \ skip # blanks
		>>pieces-next
	@pieces-slash
		row 1 - row! 0 col! >>pieces-next
	@pieces-next
		i 1 + i! <<pieces-loop
	@pieces-end-string
		row 0 == col 8 == and if >>pieces-ok
		"Pieces string too short in FEN:" parts 0 get + error
		
	@pieces-ok

	\ parts[1] is "w" or "b" to move next
	parts 1 get
	[
		[ { "w" == } { WHITE SIDE-TO-MOVE! } ]
		[ { "b" == } { BLACK SIDE-TO-MOVE! } ]
		[ else { "Bad side to move in FEN string" error } ]
	]
	cond

	\ parts[2] is castling availability - any of KQkq-
	\ set initially to none
	[ false false false false ] CASTLING!
		\ K?
		parts 2 get { "K" == } find void? if >>check-W-OOO
		CASTLING 0 true put-
	@check-W-OOO
		\ Q?
		parts 2 get { "Q" == } find void? if >>check-B-OO
		CASTLING 1 true put-
	@check-B-OO
		\ k?
		parts 2 get { "k" == } find void? if >>check-B-OOO
		CASTLING 2 true put-
	@check-B-OOO
		\ q?
		parts 2 get { "q" == } find void? if >>castling-done
		CASTLING 3 true put-
	@castling-done

	\ parts[3] is en-passant target square or '-' if none
	parts 3 get "-" == 
		{ -1 EPSQUARE! }
		::{ parts 3 get algebraic-to-square EPSQUARE! }
		ifelse
	
	\ parts[4] is halfmove number
	parts 4 get parse-int HALFMOVE!
		
	\ parts[5] is fullmove
	parts 5 get parse-int FULLMOVE!
	;
	
clear-board
"PIECES:" . PIECES . CR
"COLORS:" . COLORS . CR

31 P-BISHOP WHITE put-piece 
19 P-ROOK BLACK put-piece

4 "K" char-to-piece put-piece

print-board

"rnbqkbnr/pppppppp/8/8/8/8/PPPPPPPP/RNBQKBNR w KQkq - 0 1" set-from-fen
print-board

"rnbqkbnr/pp1ppppp/8/2p5/4P3/5N2/PPPP1PPP/RNBQKB1R b KQkq - 1 2" set-from-fen
print-board

"rnbqkbnr/pp1ppppp/8/2p5/4P3/5N2/PPPP1PPP/RNBQKB1R b kQ c3 11 24" set-from-fen
print-board

'FOO 123 make-var
"WORDS:" . CR
"FOO:" . 'FOO .dumpword . CR
"FOO!:" . 'FOO! .dumpword . CR

"FOO IS:" . FOO . CR
"SET FOO TO 'abcde'" . CR
"abcde" FOO!
"FOO IS:" . FOO . CR

"* test if-else:" . 

3 1 == { "ERROR" . } { "OK!" . } ifelse

3 1 != { "OK!" . } { "ERROR" . } ifelse

CR

\ similar to 'cond' in Scheme
\
\ condexprs is a list of [ { .. test .. } { .. call if true .. } ]
\ with optional element: [ else { .. call if no match .. } ]
: cond ( elem condexprs -- )
	@locals [ i ]
	0 i!
	@loop
		condexprs i get void? if >>nomatch
		condexprs i get 0 get 'else == if >>match 
		elem condexprs i get 0 get call if >>match
		i 1 + i! <<loop
	@nomatch
		return \ or should it be an error to match no clauses?
	@match 
		condexprs i get 1 get call return \ call matching clause
	;

'C
[
	[ { "c" == } { "it is C" . CR } ]
	[ { "a" == } { "it is A" . CR } ]
	[ else { "neither A nor B" . CR } ]
]
make-var

"a" C cond
"c" C cond
"z" C cond

"to algebraic:" . CR
"6 (g1):" . 6 square-to-algebraic . CR
"18 (c3):" . 18 square-to-algebraic . CR
"63 (h8):" . 63 square-to-algebraic . CR

"c3 (18):" . "c3" algebraic-to-square . CR
"h8 (63):" . "h8" algebraic-to-square . CR
"g1 (6):" . "g1" algebraic-to-square . CR
"a1 (0):" . "a1" algebraic-to-square . CR

: valid-rowcol? ( row col )
	row 0 >=
	row 8 <
	col 0 >=
	col 8 <
	and and and
	;
	
\ for pieces that have fixed attack spaces relative to their position (kings, knights)
\
\ square = square that piece is assumed to be on
\ moves = list of [ dRow dCol ] (delta row/col) to move from square
\
\ this only returns squares that the piece COULD attack; caller must still check if there
\ is a piece on the square or not
: gen-fixed-attacks ( square moves -- attacks )
	@locals [ row col row+ col+ ]
	[ ] attacks!
	square square-to-rowcol col! row!
	moves
	::{ ( move )
		\ calc row+dRow, col+dCol and see if that is a valid square
		row move 0 get + row+!
		col move 1 get + col+!
		row+ col+ valid-rowcol? not if >>done
		\ valid, so add square to attacks
		attacks row+ col+ square-index append attacks!
		@done
	} for-each
	
	attacks
	;

\ generate a table of all fixed attacks for a piece at each square
: gen-all-fixed-attacks ( moves -- attack-table ) 
	[ ] attack-table!
	::{ ( square )
		attack-table
			square moves gen-fixed-attacks
			append attack-table!
	} 0 1 64 for-range
	attack-table
	;
	
\ generate attack squares for sliding pieces - pieces moves from square along (dRow,dCol) direction
\
\ squares are returned as a list of empty squares + the final blocked square, or void if all are empty
: gen-sliding-attacks ( square dRow dCol -- attacks blocked )
	@locals [ row col ]
	[ ] attacks!
	square square-to-rowcol col! row!
	@loop
		row dRow + row!
		col dCol + col!
		row col valid-rowcol? not if >>off-board 
		row col square-index square!
		square piece-at P-NONE != if >>blocked 
		\ valid & empty -- add to attacks
		attacks square append drop <<loop
	@off-board
		\ all were empty and i went off side of board, so
		\ no square was blocked
		attacks void return
	@blocked
		\ square is blocked (be either side) so stop & return
		attacks square return
	;

\ generate sliding attacks for multiple direction vectors
\	movedirs is list of [ dRow dCol ]		
\
\ here, blockers is always a list (can be empty)
: gen-all-sliding-attacks ( square movedirs -- attacks blockers )
	[ ] attacks!
	[ ] blockers!
	movedirs
	::{ ( dir )
		square dir unmake drop gen-sliding-attacks \ ( attacks blocked )
		\ if blocked is not void, add to blockers list
		dup void? not
			::{ blockers swap append blockers! }
			{ drop } ifelse
			
		\ add attacks to list (use extend so i don't change object identity vs attacks
		\ defined in outer scope!)
		attacks swap extend attacks!
	} for-each
	
	attacks blockers
	;
	
\ generate attacks for king of given side at square (king does not have
\ to actually be there); these will be ALL squares the king can reach,
\ regardless of whether something is on the destination square
'KING-MOVEDIRS [ [ 1 0 ] [ -1 0 ] [ 0 1 ] [ 0 -1 ] [ 1 1 ] [ 1 -1 ] [ -1 1 ] [ -1 -1 ] ] make-var

\ pregenerated list of king attacks indexed by square
'KING-ATTACKS-BY-SQUARE [ ] make-var

\ pregenerate KING-ATTACKS-BY-SQUARE
: gen-all-king-attacks ( -- ) 
	KING-MOVEDIRS gen-all-fixed-attacks
		KING-ATTACKS-BY-SQUARE!
	;
	
\ generate attacks for knight of given side at square (knight does not have
\ to actually be there); these will be ALL squares the knight can reach,
\ regardless of whether something is on the destination square
'KNIGHT-MOVEDIRS [ [ 2 1 ] [ 2 -1 ] [ -2 1 ] [ -2 -1 ] [ 1 2 ] [ 1 -2 ] [ -1 2 ] [ -1 -2 ] ] make-var

\ pregenerated list of knight attacks indexed by square
'KNIGHT-ATTACKS-BY-SQUARE [ ] make-var

\ pregenerate KNIGHT-ATTACKS-BY-SQUARE
: gen-all-knight-attacks ( -- ) 
	KNIGHT-MOVEDIRS gen-all-fixed-attacks
		KNIGHT-ATTACKS-BY-SQUARE!
	;
	
\ move object
: new-move ( piece side squareFrom squareTo )
	\ fields: 
	\	tag 
	\	piece
	\	side
	\	squareFrom 
	\	squareTo
	\	pieceCaptured
	[ 'move piece side squareFrom squareTo P-NONE ]
	;
	
\ is obj a move object?
: move? ( obj )
	obj list? not if >>no 
	obj 0 get 'move == if >>yes
	@no false return
	@yes true return
	;

\ print a move verbosely
: print-move-long ( move )
	move move-piece move move-side piece-char "-" + puts
	move move-squareFrom square-to-algebraic puts
	move move-isCapture? { "x" puts } ifthen
	move move-squareTo square-to-algebraic .
	;
	
\ setters/getters -- for efficiency these assume move is valid - caller should
\ check with move? if unsure before calling these
: move-piece ( move -- piece ) move 1 get ;
: move-piece! ( move piece -- move ) move 1 piece put ;

: move-side ( move -- side ) move 2 get ;
: move-side! ( move side -- move ) move 2 piece put ;

: move-squareFrom ( move -- square ) move 3 get ;
: move-squareFrom! ( move square -- move' ) move 3 square put ;

: move-squareTo ( move -- square ) move 4 get ;
: move-squareTo! ( move square -- move' ) move 4 square put ;

: move-pieceCaptured ( move -- piece ) move 5 get ;
: move-pieceCaptured! ( move piece -- move' ) move 5 piece put ;
	
: move-isCapture? ( move -- result ) move move-pieceCaptured P-NONE != ;

"Making king attack table ..." . CR	
gen-all-king-attacks

"Making knight attack table ..." . CR	
gen-all-knight-attacks

\ attacks is from gen-fixed-attacks
: gen-fixed-moves ( piece square attacks side -- moves )
	[ ] moves!
	attacks
	::{ ( squareTo )
		\ see what is at squareTo
		squareTo piece-at P-NONE == if >>empty
		\ not empty - see if its opposing color
		squareTo color-at side other == if >>capture
		\ my own piece is on squareTo, so no move generated
		return
		@empty
			\ move to empty square
			moves
				piece side square squareTo new-move append moves!
			return
		@capture
			\ gen capture
			moves
				piece side square squareTo new-move
				squareTo piece-at move-pieceCaptured!
				append moves!
			return
	} for-each
	moves
	;
	
\ (attacks,blockers) is from gen-all-sliding-attacks
: gen-sliding-moves ( piece square attacks blockers side -- moves )
	[ ] moves!
	\ all the squares in attacked are empty, so make those first
	attacks
	::{ ( squareTo )
		\ always non-capturing move
		moves
			piece side square squareTo new-move append moves!
	} for-each
	
	\ now i have to check if blockers are opposite side (voids were already
	\ filtered out in gen-all-sliding-squares)
	blockers
	::{ ( squareTo )
		squareTo color-at side == if >>done \ blocked by my piece, so i'm done
		\ else its a capture
		moves
			piece side square squareTo new-move
			squareTo piece-at move-pieceCaptured!
			append moves!
		@done
	} for-each
	moves
	;
	
\ NOTE - all the gen-PIECE-moves functions are pseudo-legal - they are the literal moves
\ the piece can make, regardless of whether they put/leave their king in check

: gen-king-moves ( square side -- moves )
	P-KING square KING-ATTACKS-BY-SQUARE square get side gen-fixed-moves
	;
	
: gen-knight-moves ( square side -- moves )
	P-KNIGHT square KNIGHT-ATTACKS-BY-SQUARE square get side gen-fixed-moves
	;
	
'ROOK-MOVEDIRS [ [ 1 0 ] [ -1 0 ] [ 0 1 ] [ 0 -1 ] ] make-var

: gen-rook-moves ( square side -- moves )
	@locals [ attacks blockers ]
	\ gen attacks & blocked squares
	square ROOK-MOVEDIRS gen-all-sliding-attacks blockers! attacks!
	P-ROOK square attacks blockers side gen-sliding-moves
	\ return moves
	;

'BISHOP-MOVEDIRS [ [ 1 1 ] [ -1 1 ] [ 1 -1 ] [ -1 -1 ] ] make-var

: gen-bishop-moves ( square side -- moves )
	@locals [ attacks blockers ]
	\ gen attacks & blocked squares
	square BISHOP-MOVEDIRS gen-all-sliding-attacks blockers! attacks!
	P-BISHOP square attacks blockers side gen-sliding-moves
	\ return moves
	;
	
\ generate pseudolegal moves for side (moves are not checked to see if they
\ put the king into/leave the king in check)
: gen-pseudo-moves ( side -- moves )
	@locals [ piece ]
	[ ] moves!
	\ for each square ...
	::{ ( i )
		i piece-at piece!
		piece P-NONE == if >>done \ empty square, skip
		i color-at side != if >>done \ wrong color piece

		\ generate moves for piece
		piece P-ROOK == if >>rook 
		piece P-BISHOP == if >>bishop 
		piece P-KNIGHT == if >>knight 
		piece P-KING == if >>king 
		"Unknown piece:" piece str + error
		
		\ note: use extend to preserve object id with outer loop
		@rook  moves i side gen-rook-moves extend moves! >>done
		@bishop moves i side gen-bishop-moves extend moves! >>done
		@knight moves i side gen-knight-moves extend moves! >>done
		@king moves i side gen-king-moves extend moves! >>done
		@done
	} 0 1 64 for-range
	moves
	;
	
"2k5/8/8/8/3np3/2NK4/3RB3/8 w - - 0 1" set-from-fen
print-board

2 3 square-index WHITE gen-king-moves { print-move-long } for-each CR 
	
"1k6/5b2/1b5N/4b3/2N3b1/8/8/5K2 w - - 0 1" set-from-fen
print-board

3 2 square-index WHITE gen-knight-moves { print-move-long } for-each CR 

5 7 square-index WHITE gen-knight-moves { print-move-long } for-each CR 

"2k5/5n2/3B4/6B1/2n5/4P3/1P6/1K6 w - - 0 1" set-from-fen
print-board

3 2 square-index BLACK gen-knight-moves { print-move-long } for-each CR 

"k7/8/8/1n6/8/1R3b2/8/4K3 w - - 0 1" set-from-fen
print-board

2 1 square-index WHITE gen-rook-moves { print-move-long } for-each CR

"3k4/8/8/r3r3/1B6/8/8/4K3 w - - 0 1" set-from-fen
print-board

3 1 square-index WHITE gen-bishop-moves { print-move-long } for-each CR

"* Test gen-pseudo-moves ..." . CR

"3k4/8/8/r3r3/1B6/8/8/4K3 w - - 0 1" set-from-fen

WHITE gen-pseudo-moves { print-move-long } for-each CR
