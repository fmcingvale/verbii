 
\ Various numeric functions
\
\ Copyright (c) 2022 Frank McIngvale, see LICENSE

\ ======================================================================
\ Parsing & printing of hex values
\ ======================================================================

\ convert char h from hex to int
: hex-char-to-int @( h -- i )
	h "0" >= 
	h "9" <=
	and if >>digit 

	h "a" >=
	h "h" <=
	and if >>hexlower 

	h "A" >= 
	h "H" <=
	and if >>hexupper

	"Bad hex digit: " h + error

	@digit h ord "0" ord - return
	@hexlower h ord "a" ord - 10 + return
	@hexupper h ord "A" ord - 10 + return
	;

\ parse hex string to integer -- hex must NOT have leading "0x"; ONLY hex digits
: parse-hex @( hex -- i )
	\ fold
	hex
	@{ @( accum h ) 
		h hex-char-to-int 
		accum 16 * + 
	} 0 fold
	;

\ 4-bit value to hex digit
: int4-to-hex ( n -- h )
	dup 9 <= if >>digit 
	dup 16 < if >>hex 
	"Bad value in int4-to-hex:" swap str + error

	@digit "0" ord + chr return
	@hex 10 - "a" ord + chr
	;

\ 8-bit value to 2 hex digits )
: int8-to-hex ( n -- h )
	16 /mod int4-to-hex swap int4-to-hex +
	;

\ 16-bit value to 4 hex digits
: int16-to-hex ( n -- h )
	256 /mod int8-to-hex swap int8-to-hex +
	;

\ 32-bit value to 8 hex digits )
: int32-to-hex ( n -- h )
	65536 /mod int16-to-hex swap int16-to-hex +
	;

\ shortcut - int to hex string
\ - always unsigned
\ - does NOT prepend "0x", only returns the hex digits
: str.hex ( val ) int32-to-hex ;

\ ======================================================================
\ More bit operations (all 32-bit)
\ ======================================================================

\ rotate X left by K bits
: bit-rotl @( x k )
	\ (x << k) | (x >> (32 - k))
	x k bit-shl
	x 32 k - bit-shr
	bit-or
	;
	
: bit-low32 @( val ) val 4294967295 bit-and ;

