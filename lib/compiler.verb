(
	Verbii compiler, written in verbii
	
	Copyright (c) 2022 Frank McIngvale, see LICENSE

	*** Read bootstrap/READ-ME-BEFORE-RUNNING.txt before editing this code! ***
	***       Changing this file can easily break the entire system!        ***
)

\ **TODO** change this to use the stream interface

( --- reader interface, used by compiler, but may be generally useful
	usage:
		" ... " reader-open-string
		or
		filename reader-open-file

		then reader-next until it returns null
)

var READER_TEXT 1 ( string )
var READER_TEXT_LEN 1
var READER_POS 1

( note - the reader-open-* functions discard any current input )

: reader-open-string ( string )
	READER_TEXT set!
	READER_TEXT ref length READER_TEXT_LEN set!
	0 READER_POS set!
	;

: reader-open-file ( filename )
	file-read reader-open-string
	;

\
\ general note for below: for consistency, when there is no return value due to EOF,
\ void is returned. although technically only a few places need to distinguish null from void,
\ using void throughout I think will be less confusing in the long run
\

: reader-peek-char ( -- char; get next char [string] from reader without advancing stream )
	READER_POS ref READER_TEXT_LEN ref >= if >>eof
	READER_TEXT ref READER_POS ref get ( char )
	return
	@eof 
		void \ return void on eof
	;

: reader-next-char ( -- char; get next char [string] from reader stream or void on eof )
	READER_POS ref READER_TEXT_LEN ref >= if >>eof
	READER_TEXT ref READER_POS ref get ( char )
	READER_POS ref 1 + READER_POS set! ( char; ++pos )
	return
	@eof
		void
	;

: reader-next-chunk ( -- next string of either all whitespace or non-whitespace )
		\ "reader-next-chunk" . CR
	"" ( string )
	\ see what next chunk starts with ...
	reader-peek-char
	dup void? if >>drop-return
	dup whitespace? if >>ws 
	\ else, non-whitespace
	drop
	@non-ws-loop ( string )
	reader-peek-char ( if next is whitespace, do NOT want to read it )
	dup void? if >>drop-return
	dup whitespace? if >>drop-return
	drop reader-next-char + <<non-ws-loop ( append to string and continue )

	\ read whitespace chunk
	@ws
	drop
	@ws-loop ( string )
	reader-peek-char
	dup void? if >>drop-return
	dup whitespace? not if >>drop-return
	drop reader-next-char + <<ws-loop

	@drop-return ( string char )
		drop return
	;

: reader-next ( -- symbol ; get next word from reader stream, or void on eof )
	"" ( word )
	@skipws ( skip leading whitespace )
		reader-peek-char
		dup void? if >>void-on-read 
		ord 32 > if >>word-loop
		reader-next-char ( word char; ++pos )
		drop
		<<skipws

	@void-on-read ( word void )
		drop >>eof

	@word-loop ( word )
		( i want to preserve any whitespace after word [e.g. for string parsing], so peek first )
		reader-peek-char ( word char )
		dup void? if <<void-on-read
		( check for whitespace )
		dup ord 32 <= if >>space  ( treat anything from ' ' to NUL as whitespace )
		( not whitespace, read char, append and continue )
		drop reader-next-char + <<word-loop

	@space ( word char )
		drop ( fall through to return word below )

	@eof ( word ) 
		( return word, void on end of file )
		dup length 0 == if >>isvoid
		string->symbol return ( return word as symbol )

	@isvoid drop void return
	;

: digit? ( string|symbol -- true|false; is single char a digit? )
	dup string? if >>string
		dup '0 < if >>notdigit
		dup '9 > if >>notdigit
		drop true return  ( symbol match )
	@string
		dup "0" < if >>notdigit
		dup "9" > if >>notdigit
		drop true return
	@notdigit drop false
	;

: any-digits? ( string|symbol -- true|false; does string/symbol contain ANY digits? )
	{ digit? } any?
	;

: ltrim-char ( string char -- if char (single-char string) is at front of string, remove it )
	over length 0 != if >>do-trim
	drop return \ 0-length string, nothing to do
	@do-trim
	>L 0 +get L> == if >>trim
	return
	@trim 1 -1 slice
	;

: ltrim-sign ( string -- string with leading "+" or "-" removed [max 1] )
	dup length 0 != if >>do-trim
	return \ 0-length string
	@do-trim
	0 +get "+" == if >>trim
	0 +get "-" == if >>trim
	return
	@trim 1 -1 slice
	;

: ltrim-digits ( string -- string with leftmost digits removed )
	@loop
		dup length 0 == if >>done
		0 +get digit? not if >>done
		1 -1 slice <<loop
	@done
	;

: int-string? ( string -- true|false if string can be parsed as integer )
	dup any-digits? not if >>nodigits ( must have digits, not just +/- )
	ltrim-sign
	ltrim-digits
	length 0 == return
	@nodigits 
		drop false
	;

: int-symbol? ( like above but for symbols )
	unmake make-string 
	int-string?
	;

: float-string? ( string -- true|false if string can be parsed as float )
	( NOTE - this will ALSO match integers, so use int-string? first to rule those out )
	dup any-digits? not if >>nodigits ( must have digits, not just +/-/./e )
	ltrim-sign
	ltrim-digits
	"." ltrim-char
	ltrim-digits
	"e" ltrim-char
	ltrim-sign
	ltrim-digits
	length 0 == return
	@nodigits 
	drop false
	;

: float-symbol? ( as above but for symbols )
	unmake make-string float-string?
	;

\ add each name to dict with value set to size of dict at time name was added
\ (i.e. adding [ "a" "b" "c" ] to an empty dict gives a=0, b=1, c=2)
\ note: symbols are accepted and converted to strings
: dict-add-indexed-names @( dict names -- dict-out )
	@locals [ i ]
	0 i!
	@loop
		i names length >= if >>done 
		dict names i get str dict length put drop \ dict[name[i]] = len(dict)
		i 1 + i! <<loop
	@done
		dict
	;
	
( make literal """ which is not parseable itself )
var TRIPLEQUOTE 1
34 34 34 3 make-symbol TRIPLEQUOTE set!

\ parse next word from input into runnable object
: syntax-next ( -- obj )
	reader-next
		 \ "syntax-next:" . dup . CR
	dup void? if >>void
	dup '( == if >>comment
	dup '\ == if >>line-comment
	dup '{ == if >>lambda
	dup '@{ == if >>closure
	dup '[ == if >>list
	dup '@[ == if >>capture-list
	dup ': == if >>worddef
	dup 'def == if >>worddef ( synonym for ':' )
	dup 'var == if >>var
	dup 'del == if >>del
	dup 'true == if >>true-literal
	dup 'false == if >>false-literal
	dup 'null == if >>null-literal

	( look for triple-quoted string first )
	dup 0 3 slice TRIPLEQUOTE ref == if >>triplestring
	( now single quoted string )
	dup 0 get '" == if >>string ( "... )
	\ test for correct format before passing to parse-int or parse-float since those functions
	\ will happily parse something like "123abc" as 123 )
	dup int-symbol? if >>integer
	dup float-symbol? if >>float
	dup 0 get '# != if >>nomatch dup 1 -1 slice float-symbol? if >>float-literal
	@nomatch
		( leave unprocessed word on stack )
		return
	@void
		return ( leave void on stack )
	@worddef
		drop syntax-define-word return
	@var
		drop syntax-var return
	@del
		drop syntax-del return
	@comment
		drop syntax-comment syntax-next return ( skip comment and return NEXT object )
	@line-comment
		drop syntax-line-comment syntax-next return ( skip comment and return NEXT object )
	@lambda
		drop syntax-lambda return
	@closure
		drop syntax-closure return
	@list
		drop syntax-list return
	@capture-list
		drop syntax-capture-list return
	@triplestring
		( call syntax-triple-string with first piece on stack )
		syntax-triple-string return
	@string
		( call syntax-string with first piece on stack )
		syntax-string return
	@integer
		parse-int return
	@float
		parse-float return
	@float-literal ( #NN.NN form )
		1 -1 slice parse-float return
	@true-literal drop true return
	@false-literal drop false return
	@null-literal drop null return
	;

\ if list is [ ,,inline obj1 .. objN ] then push obj1 .. objN to the stack
\ and remove of list. else leave list alone.
: flatten-if-inline ( list -- list or items )
	dup length 0 == if >>done \ empty list, leave on stack
	dup 0 get ',,inline == if >>flatten
	return \ not ,,inline, leave on stack
	@flatten ( list )
		\ drop ,,inline and push rest of list to stack
		1 -1 slice unmake drop return
	@done ( list )
	;

\ assumes @( was just read, parses until ) as an object list, prepended with ,,declargs
: syntax-parse-decl-arglist
	@locals [ outlist ]
	',,declargs 1 make-list outlist!
	@loop
		syntax-next
		dup void? if >>eof
		dup ') == if >>done
		\ else append object and continue
		outlist swap append outlist! <<loop
	@eof
		drop "End of input inside @(" error
	@done
		drop outlist
	;
	
\ assumes ':' was just read
: syntax-define-word
	@locals [ SP-start name ]
	syntax-next ( name must be next )
	dup symbol? not if >>badname
	\ call syntax-next repeatedly until ';' is found. this will leave all parsed
	\ words on stack that i'll then make into a list. easy way to do this is remember
	\ starting value of SP then subtract from SP at end to get # of items parsed.
	name! SP SP-start!
	@loop
		syntax-next ( get next processed object )
		dup void? if >>eof
		dup '@( == if >>arglist \ ) <- freaks out notepad++ styling here
		dup '; == if >>endword
		dup list? if >>list \ lists may need special handling, see below
		<<loop
	@arglist ( obj )
		\ i do not check here to see if i have multiple @( .. ) constructs; that can
		\ be handled at the rewrite stage
		drop
		syntax-parse-decl-arglist <<loop
		
	\ when i get a list i need to see if it is [ ,,inline ... ] and flatten it if so
	@list ( list )
		flatten-if-inline <<loop

	@badname str "Invalid name after ':' : " swap + error
	@eof "Unexpected end of input looking for ';'" error
	@endword
		drop ( drop ; )
		\ does word end with 'return? if not, add it
		dup 'return == if >>finish
		\ add return
		'return
	@finish
		SP-start SP - 1 - ( calc number of items )
		make-list
		( save names & compiled words )
		name save-defined-word
		syntax-next ( save-defined-word has no return value, so return NEXT object )
	;

\ var NAME COUNT -- 'var' was already read
: syntax-var
	syntax-next ( name should be next )
	dup symbol? not if >>badname
	'' swap + ( make quoted name )
	syntax-next ( count should be next )
	dup int? not if >>badcount

	\ general note for here and below -- syntax-next has to return ONE object to
	\ avoid complicating all callers, so when a syntax element expands to multiple
	\ objects, those objects are returned as a list: [ ,,inline obj ... ]. this
	\ list will then be flattened as needed, but intermediate routines can just pass
	\ the list as-is and not worry about it representing multiple objects.
	
	',,inline -rot ( ,,inline name count )
	',,var ( ,,inline ,,var name count ,,var )
	4 make-list
	 \ "SYNTAX VAR MADE:" . .S CR
	return

	@badname str "Bad name in var: " swap + error
	@badcount str "Bad count in var: " swap + error
	;

\ del NAME -- 'del' was already read
: syntax-del
	syntax-next ( name must be next )
	dup symbol? not if >>badname
	'' swap + ( make quoted name )
	\ as with var, i need to return multiple words, so return 
	\ [ ,,inline name ,,del ]
	',,inline swap ( ,,inline name )
	',,del ( ,,inline name ,,del )
	3 make-list 
	return

	@badname str "Bad name in del: " swap + error
	;

: syntax-string ( first string part already on stack *as symbol* )
	( convert first since reader-next-char returns strings )
	symbol->string
		
	( see if first string part ends with " )
	dup -1 get ord 34 == ( use ascii value for " )
		over length 1 >
		and
		if >>endstring ( first part begins and ends with " so string is complete )

	( now switch to character mode to preserve all inner whitespace )
	@loop ( string )
		reader-next-char ( string char )
		dup void? if >>eof ( EOF in string, error )
		dup ord 34 == if >>quote ( test against ASCII value for " to avoid any parsing weirdness )
		( else, append regular char to string )
		+ <<loop
		
	@quote ( string " )
		+ ( append " to string )

		( if whitespace is next, then string is complete )
		reader-peek-char
		dup void? if >>endquote ( EOF ok AFTER end quote )
		dup whitespace? if >>endquote
		( else, keep reading more )
		drop <<loop

	@endquote drop >>endstring

	@eof ( string char ) 
		drop "Unexpected end of input inside string: " swap + error

	@endstring ( string -- string without quotes )
		dup length 2 - 1 swap slice ( remove quotes )
	;
	
: syntax-triple-string ( first-piece-symbol -- string )
	\ convert to string first ...
	symbol->string
	\ special case of only one piece ... """..."""
	dup length 6 >=
	over -3 -1 slice TRIPLEQUOTE ref symbol->string == and if >>return

	\ else, keep first-piece on stack and add more chunks until i find one
	\ that ends in """
	@read-loop
		reader-next-chunk
		dup void? if >>error-eos
		+
		dup -3 -1 slice TRIPLEQUOTE ref symbol->string == if >>return
		<<read-loop

	@error-eos
		"String ended inside triple-quoted string: " swap + error

	@return ( remove triple quotes from both sides )
		dup length 3 swap 6 - slice
	;

\ parse { .. } and return lambda
\	- assumes { was just read & dropped by caller
\	- appends 'return to end, if not already present
: syntax-lambda
	0 make-list 
	@loop
		\ note - any inner lambdas will be handled automatically by syntax-next,
		\ so don't have to check for "{" here
		syntax-next
		dup void? if >>eof
		dup '} == if >>closebrace
		append ( else append to list and continue ) <<loop
	@eof
		"Unexpected end of input inside { .. }" error
	@closebrace
		drop \ drop '} 
		dup -1 get 'return == if >>done \ return already present
		'return append \ add return to end
	@done
		make-lambda return
		;

: syntax-list ( assumes [ was just read & dropped )
	0 make-list 
	@loop ( list )
		\ note - any inner lists/lambdas will be handled automatically by syntax-next,
		\ so don't have to check for { or [ here
		syntax-next
		dup void? if >>eof
		dup '] == if >>closelist
		append ( else append to list and continue ) <<loop
	@eof
		"Unexpected end of input inside [ .. ]" error
	@closelist
		drop
		;

\ parses @[ .. ] into [ ,,inline ... ]
\
\ assumes @[ was just read, parses until ] as an object list, prepended with ,,inline
\
\ expanding literal lists back into stack operations allows variable (name) substitution to work
\ on them. obviously slower than literal lists that are just deepcopied on instantiation.
: syntax-capture-list
	@locals [ outlist ]
	',,inline 1 make-list outlist!
	@loop
		syntax-next
		dup void? if >>eof
		dup '] == if >>done
		\ else append object and continue
		outlist swap append outlist! <<loop
	@eof
		drop "End of input inside @[" error
	@done
		drop 
		\ add code to remake list at runtime (so captures will
		\ have been evaluated first)
		outlist outlist length 1 - append 'make-list append outlist!
		outlist
	;
	
\ parse ( ... ) comment, with nesting. assumes "(" was just read.
: syntax-comment
	@locals [ nesting comment-text ]
	"" comment-text!
	1 nesting!
	@loop
		reader-next ( puts symbol on stack )
		dup void? if >>eof
		( add to comment text )
		dup symbol->string comment-text " " + swap + comment-text! 

		dup ') == if >>closeparen
		dup '( == if >>openparen \ ) <- to stop notepad++ highlighter from messing up
		drop <<loop ( discard and continue )
	@closeparen
		nesting 1 - nesting!
		nesting 0 == if >>end
		drop <<loop
	@openparen
		nesting 1 + nesting!
		drop <<loop
	@end
		drop return
	@eof
		comment-text 0 40 slice
		"Unexpected end of input inside comment: " swap + error
	;
	
: syntax-line-comment ( assumes "\" was just read )
	( read *characters* until EOL found )
	@loop
		reader-next-char
		( if end of stream, "\n" or "\r", then i'm done )
		dup void? if >>eol
		dup ord 13 == if >>eol
		dup ord 10 == if >>eol
		drop <<loop ( discard char and continue )
	@eol
		drop ( discard char and return )
	;

\ parses @{ .. } into [ ,,closure ... ]
\
\ assumes @{ was just read, parses until } as an object list, prepended with ,,closure
\ 
\ ensures there is a 'return at the end
: syntax-closure
	@locals [ outlist ]
	',,closure 1 make-list outlist!
	@loop
		syntax-next
		dup void? if >>eof
		dup '} == if >>done
		dup '@( == if >>arglist \ ) <- paren to fix notepad++ syntax highlighting
		\ else append object and continue
		outlist swap append outlist! <<loop
	@arglist
		\ like with define word, don't worry about tracking if i already
		\ got @( .. ), just parse it for now and will handle later
		drop
		syntax-parse-decl-arglist 
		outlist swap append outlist! <<loop
	@eof
		drop "End of input inside @{" error
	@done
		drop \ drop '}
		\ ensure 'return is at end
		outlist -1 get 'return == if >>exit
		outlist 'return append outlist! \ add return
	@exit
		outlist
	;
	
: serialize-escape-string ( string -- string with whitespace escaped )
	{ 
		( char )
		dup ord 32 == if >>space
		dup ord 37 == if >>percent
		dup ord 9 == if >>tab
		dup ord 10 == if >>lf 
		dup ord 13 == if >>cr 
		\ not a special char - return as-is
		return

		\ note -- "%" cannot appear in the strings here nor the serialize/deserialize
		\ loop - would mess up the replacement strings. so use % == chr(37) instead of "%" )
		@space ( char )
			drop 37 chr "32" + ( "%32" )
			return

		@tab ( char )
			drop 37 chr "09" + ( "%09" )
			return

		@percent ( char )
			drop 37 chr "37" + ( "%37" )
			return
			
		@lf ( char )
			drop 37 chr "10" + ( "%10" )
			return
		@cr 
			drop 37 chr "13" + ( "%13" )
	} map
	;

\ general note: the following is NOT intended to be a general-purpose serialization format.
\ 				its only purpose is to be able to serialize byte-compiled verbii source.
\				primarily it is for compiler.verb and init.verb, but is general enough to
\				serialize any verbii source file.
\
\				as a consequence, there is no need to serialize anything that cannot exist
\				in source form. this includes:
\					* void - no source format (void is a word)
\					* closure - no source format - requires :: at runtime
\					* dict - no source format - requires make-dict or ,,new-dict at runtime
\
\				technically floats are not needed in compiler.verb nor init.verb, however,
\				to allow this to be used for ANY verbii source (i.e. to cache compilations)
\				they are allowed here.

: serialize-object ( obj -- ; serialize obj to stdout )
	dup int? if >>int
	dup float? if >>float
	dup string? if >>string
	dup symbol? if >>symbol
	dup list? if >>list
	dup lambda? if >>lambda
	dup bool? if >>bool
	dup null? if >>null
	repr "Unknown obj in serialize-object:" swap + error
	@int ( i NNN )
		"i" . str puts CR return
	@float ( f NN.NNN )
		"f" . str puts CR return
	@bool ( b true|false )
		"b" . str puts CR return
	@null ( n )
		drop "n" . CR return
	@string ( s SSS )
		"s" . serialize-escape-string puts CR return
	@symbol ( y SSS - symbols cannot contains spaces, \n, etc. so no escaping needed )
		"y" . str puts CR return
	@list ( "L length" then serialized items )
		"L" . dup length str puts CR
		{ serialize-object } for-each ( items )
		return
	@lambda ( "F" then list serialized )
		"F" puts CR
		unmake serialize-object ( serialize list )
		return
		;

: save-defined-word ( list name )
	swap 2 make-list ( [ name list ] )
	COMPILED_NAMELISTS ref swap append drop
	;

\ take a list: [ ,,declargs arg ... -- local ... ]
\ and return lists of [ arg ... ] and [ local ... ]
: compile-split-declargs ( list -- args locals )
	@args [ list ]
	@locals [ i args locals parsing-locals ]
	[ ] args!
	[ ] locals!
	false parsing-locals! \ have i seen the '--' ?
	1 i! \ skip ,,declargs
	@loop
		i list length >= if >>done
		list i get '-- == if >>got-dashes 
		\ else add to either args or locals list
		parsing-locals if >>add-to-locals
		args list i get append args! >>next \ add to args
	@add-to-locals ( obj )
		locals list i get append locals! >>next \ add to locals 
	@got-dashes
		true parsing-locals! \ switch to parsing locals, fall through to next
	@next
		i 1 + i! <<loop
	@done 
		args locals
		;
		
\ takes a compiled objlist and collects:
\	[ ,,declargs arg .. -- local .. ] => args & locals
\		(the compiler generates this from @( arg ... -- local ... )
\	@args [ ... ] => args
\	@locals [ ... ] => locals
\
\ returns rewritten objlist with the @args & @locals removed.
\
\ it is valid to have BOTH @( ... ) and @args/@locals -- the names will be
\ merged into a single args/locals lists
: compile-collect-args-locals ( objlist -- new-objlist args locals )
	@args [ objlist ]
	@locals [ new-objlist args locals i ]
		\ "COMPILE COLLECT ARGS LOCALS:" . .S CR
		\ "	objlist:" . objlist str . CR
	[ ] new-objlist!
	[ ] args!
	[ ] locals!
	0 i!
	@loop
		objlist i get ( objlist[i] )
		dup void? if >>done \ i >= length
		dup list? if >>check-list
		dup '@args == if >>args-list 
		dup '@locals == if >>locals-list
		\ else regular obj, copy to new list
		>>keep-obj
	@check-list ( obj )
		\ see if this is [ ,,declargs ... ]
		dup length 0 == if >>keep-obj
		dup 0 get ',,declargs != if >>keep-obj
		\ it is ,,declargs, so split into args,locals
		compile-split-declargs ( args locals )
		locals swap + locals!  \ append locals & args to my lists 
		args swap + args! >>next
	@keep-obj ( list )
		new-objlist swap append new-objlist! >>next
	@args-list ( obj )
		drop
		\ just saw @args so a list must be next
		i 1 + i!
		objlist i get
		dup void? if >>missing-args \ end of input
		dup list? not if >>missing-args 
		\ add list to anything collected so far from @( .. )
		args swap + args! >>next
	@missing-args
		"Expecting list after @args in:" objlist str + error
	@locals-list ( obj )
		drop
		\ list must be next
		i 1 + i!
		objlist i get
		dup void? if >>missing-locals
		dup list? not if >>missing-locals
		\ add to any existing locals from @( .. )
		locals swap + locals! >>next 
	@missing-locals
		"Expecting list after @locals in:" objlist str + error
	@next
		i 1 + i! <<loop
	@done ( void )
		drop new-objlist args locals
		;
		
\ rewrites an objlist to translate arg/local names including capturing outer vars (closures)
\
\ selfmap should be set to an empty dict at the toplevel
\
\ ASSUMPTIONS:
\	1. If objlist from { .. } or @{ .. } it must end with a 'return (the normal parsing code handles this)
\		[eventually rewriting of lists will be possible, so can't assume objlist is code and add the return here]
\	2. closures must be marked with @{ ... } ; will NOT transform unmarked lambdas since they might
\		be used in other binding constructs
\	3. does NOT (yet) rewrite lists so dynamic code cannot use @args/@lists nor create closures
\		(user code can manually do closures with :: of course)
\
\ does these things:
\	1. scans objlist (non-recursively) for @args, @locals, and/or [ ,,declargs ... ]
\	2. rewrite objlist to do:
\		a. alloc locals (LP nr - LP!)
\		b. pop args (L> L> ...)
\		c. rewrite symbols to their code sequences:
\			(i) if name in args/locals, rewrite to (LP i + ref) or (LP i + set!)
\			(ii) if name in selfmap, rewrite to (self i get) or (self i rot put drop)
\		d. if an inner lambda is found ( @{ ... } -- @ is required for backward compat with { .. } obj :: code )
\			(i) recursively rewrite lambda, passing selfmap + my args+locals as its selfmap
\			(ii) add binding of selfmap + my locals as self for inner lambda
: compile-rewrite-args-locals @( objlist selfmap -- new-objlist )
	@locals [ argnames localnames local-refmap local-setmap i self-setmap ]
			
		\ scan objlist for , @( ... ), @args, @locals
		objlist
		compile-collect-args-locals localnames! argnames! new-objlist!
			
		\ note: even if i have no @args/@locals, i can still have names that resolve to selfmap
		\       - could also have inner closures that take their own args

		\ set objlist to rewritten list without @args/@locals
		new-objlist objlist!
		\ clear new-objlist now
		[ ] new-objlist!
		
		\ make preamble code to:
		\	1. alloc spaces for @locals
		\	2. pop args to locals
		\ note this will put args starting at LP and locals starting at LP + #args
		
		\ 1. alloc locals (LP #localnames - LP!)
		localnames length 0 == if >>popargs \ omit code if no locals
		'LP localnames length '- 'LP! 4 make-list new-objlist!
	@popargs
		\ 2. push args to locals (adding code to new-objlist)
		argnames { ( accum elem ) drop '>L append } new-objlist fold new-objlist! 
		
		\ make a map of arg|local name -> index into locals
		[ ] make-dict argnames localnames + dict-add-indexed-names local-refmap!
		\ make a map of name! as well to replace setters
		[ ] make-dict 
			argnames { str "!" + } map 
			localnames { str "!" + } map 
			+ dict-add-indexed-names local-setmap!
		
		\ for each name in selfmap, make name! in self-setmap with same index
		[ ] make-dict self-setmap!
		selfmap
		@{ @args [ key val ]
			self-setmap key "!" + val put drop
		} for-each-dict
		
	\ rewrite objlist replacing arg|local symbols with appropriate code sequences and creating
	\ closures as needed
	0 i!
	@rewrite-loop
		i objlist length >= if >>done 
		objlist i get symbol? if >>symbol
		objlist i get list? if >>list
		\ everything else, copy as-is
	@copy-element
		new-objlist objlist i get append new-objlist! >>next
		
	@symbol
		objlist i get 'return == if >>rewrite-return
		\ NOTE: local scope is checked BEFORE outer scope so that the inner scope can redefine
		\ names and see the intended (local) value instead of the outer one
		\ example:
		\		: fn @( n ) @{ @args [ n ] <- here, n and n! should see the LOCAL names not the outer n, n! }
		
		\ is symbol (as string) in local-refmap?
		local-refmap objlist i get str get void? not if >>rewrite-local-ref
		\ check for local setter
		local-setmap objlist i get str get void? not if >>rewrite-local-set
		\ is symbol (as string) in selfmap?
		selfmap objlist i get str get void? not if >>rewrite-self-ref
		\ check for self setter
		self-setmap objlist i get str get void? not if >>rewrite-self-set
		
		\ else not in either list, keep as-is
		<<copy-element
		
	@rewrite-local-ref
		\ add (LP i + ref) to new-objlist
		new-objlist
		'LP local-refmap objlist i get str get '+ 'ref 4 make-list
		+ new-objlist! >>next
		
	@rewrite-local-set
		\ add (LP i + set!) to new-objlist
		new-objlist
		'LP local-setmap objlist i get str get '+ 'set! 4 make-list
		+ new-objlist! >>next
	
	@rewrite-self-ref
		\ add (self i get) to new-objlist
		new-objlist
		'self selfmap objlist i get str get 'get 3 make-list
		+ new-objlist! >>next
	
	@rewrite-self-set
		\ add (self i rot put drop) to new-objlist
		new-objlist
		'self self-setmap objlist i get str get 'rot 'put 'drop 5 make-list
		+ new-objlist! >>next
		
	@rewrite-return
		\ if no args/locals then skip -- no frame to remove
		argnames length localnames length + 0 == if >>simple-return
		
		\ replace return with code to remove local frame
		new-objlist
		'LP argnames length localnames length + '+ 'LP! 'return 5 make-list
		+ new-objlist! >>next
		
	@simple-return
		\ no frame, so just keep return
		new-objlist 'return append new-objlist! >>next
		
	@list
		\ look for [ ,,closure ... ] (parsed from @{ .. })
		objlist i get length 2 < if <<copy-element \ list too short, can't be closure
		objlist i get 0 get ',,closure == if >>rewrite-inner-closure \ got [ ,,closure .. ]
		<<copy-element \ not a special list, so just copy element

	@rewrite-inner-closure
		\ recursively parse list, passing a selfmap that is my selfmap + my locals
		selfmap deepcopy \ don't alter my selfmap
		( inner-selfmap )
		argnames dict-add-indexed-names \ add arg names first -- they are first on locals stack
		localnames dict-add-indexed-names \ .. then locals
		( inner-selfmap )
		objlist i get 1 -1 slice \ remove ,,closure
		swap ( objlist inner-selfmap )
		compile-rewrite-args-locals ( new-objlist )
		make-lambda \ turn new-objlist back into lambda
		new-objlist swap append new-objlist! \ add lambda to new-objlist
		\ now create code to make runtime binding for new lambda
		\ want self = [ self0 .. selfN arg0 .. argN local0 .. localN ]
		\ so code will be:
		\	[ ] self + ( shallow-copy self )
		\	#args #locals + copy-locals-to-list + ( self + locals ) ::
			
		\ NOTE! have to check for empty selfmap since referencing a non-existent self is an error
		selfmap length 0 == if >>inner-closure-no-selfmap
		
		\ case 1: my self is bound so concatenate my locals to self
		0 'make-list 'self '+ argnames length localnames length + 'copy-locals-to-list '+ '::
		8 make-list new-objlist swap + new-objlist! \ add self-making code to new-objlist
		>>next
		
	@inner-closure-no-selfmap
		\ case 2: no selfmap so just pass on my locals
		argnames length localnames length + 'copy-locals-to-list '::
		3 make-list new-objlist swap + new-objlist! \ add self-making code to new-objlist
		>>next
		
	@next
		i 1 + i! <<rewrite-loop
	
	@done
		new-objlist return
	;

\ take a parsed objlist, presumably from byte-compile, and perform finalization:
\	* ensure objlist ends with 'return'
\	* transform @args [ .. ] and @locals [ .. ] into code and handles closure creation
\
\ returns a new list, does NOT modify objlist
: compile-finalize-word ( objlist -- newlist )
		\ "FINALIZE WORD:" . .S CR
	\ dup length 0 > if >>not-empty
	\ objlist is empty; newlist is [ return ]
	\ drop 'return 1 make-list >>transform-args-locals

	\ @not-empty ( objlist )
	\ 	dup -1 get 'return != if >>add-return
		\ already has return at end
	\ 	>>transform-args-locals

	\ @add-return ( objlist )
	\ 	deepcopy \ don't modify original
	\ 	'return append

	@transform-args-locals ( objlist )
		\ turn names into code sequences
		[ ] make-dict ( toplevel has empty selfmap ) compile-rewrite-args-locals
			\ "AFTER REWRITING:" . .S CR
	;
	
var COMPILED_NAMELISTS 1

\ take the parsed COMPILED_NAMELISTS and perform finalization on the words.
\ see compiled-finalize-wprd for details. saves finalized lists back to COMPILED_NAMELISTS
: compiler-finalize-all-words ( -- )
		\ "FINALIZE ALL WORDS" . CR
	0
	@loop ( i )
		dup COMPILED_NAMELISTS ref length >= ( i >= #lists ) if >>done 
		COMPILED_NAMELISTS ref over get ( i lists[i] )
			\ "READY FOR PAIR:" . .S CR
		1 get ( i objlist )
			\ "REWRITING OBJLIST:" . dup . CR
		compile-finalize-word ( i new-objlist )
		over COMPILED_NAMELISTS ref swap get ( i newlist LISTS[i] )
			\ "READY TO PUT:" . .S CR
		1 rot put drop ( i )
		1 + <<loop ( ++i )
	@done
		\ "FINAL NAMELISTS:" . COMPILED_NAMELISTS ref . CR
		drop
	;

: byte-compile ( -- names wordlists ; byte-compile source from current reader stream )
	\ do NOT store compiled words into interpreter since that would prevent compiling
	\  any code that contained a name that was already defined -- for example when
	\  compiling this file
	\
	\ instead, store as a list of [name,wordlist] to COMPILED_NAMELISTS

	0 make-list COMPILED_NAMELISTS set!

	\ i need to count the number of parsed words that DID NOT go into COMPILED_NAMELISTS, so
	\ save starting SP so I can count at the end
	SP >L ( L0=SP at start )
	@loop
	( read all parsed objects then make into a list )
	syntax-next
	dup void? if >>done 
	dup list? if >>list
	<<loop
	@list
	\ **TODO** change this to use flatten-if-inline
	\ check for [ ,,inline ... ] and flatten
	flatten-if-inline <<loop
	@done
	drop
	( count # items added to stack and make list )
	L> SP - 1 - make-list
	( save this code under __main__ )
	'__main__ save-defined-word
	( perform finalization of all words )
	compiler-finalize-all-words
	( leave list of [ name list ] on stack )
	COMPILED_NAMELISTS ref
	;

: byte-compile-file ( filename -- list of [ name list ] ; byte-compile filename and leave outputs on stack )
	reader-open-file byte-compile
	;

: byte-compile-string ( text -- list of [ name list ] ; byte-compile filename and leave outputs on stack )
	reader-open-string byte-compile
	;

: serialize-compiled-output
	( take output of byte-compile-* and serialize to stdout )

	( each obj is [ name wordlist ] )
	{ "W" . dup 0 get str puts CR 1 get serialize-object } for-each 
	;

( compiles filename and writes serialized form to stdout. does NOT alter interpreter words )
: compile-and-serialize ( filename -- )
	byte-compile-file ( this will be all code other than word definitions )

	( that left list of [ name list ] on stack )

	( this will all be written to a list )
	"L" . dup length str puts CR

	( serialize )
	serialize-compiled-output
	;

( compiles string and loads words into current interpreter )
: compile-and-load-string ( string -- )
	byte-compile-string
	( now load the [ name wordlist ] pairs into interpreter )
	{ dup 1 get swap 0 get make-word } for-each
	;


\
\ compile & load the given file. if "filename.b" exists, and is the same age (or newer), 
\ then it is loaded instead of recompiling filename.
\
\ if "filename.b" does not exist, or is older than filename, then filename is
\ compiled and the result saved to "filename.b"
\
\ note this does NOT run the resulting __main__ -- it is the caller's responsibility
\ to run __main__ (or not) and to delete __main__ when complete
\
: cached-compile-and-load ( filename -- )
		\ "cached-compile-and-load of: " . dup . CR
	\ see if filename.b exists
	dup ".b" + file-exists? not if >>do-compile \ does not exist, must compile filename
		\ ".b file exists" . CR
	\ see if mtime of filename > filename.b (i.e. filename.b out of date)
	dup file-mtime over ".b" + file-mtime ( filename mtime mtime.b )
	> if >>do-compile \ .b is out of date, must recompile
		\ ".b file is current" . CR
	\ .b file is current, so load it
	".b" + deserialize return \ load words into interpreter & return

	@do-compile ( filename )
		 \ "(re) compiling " . dup . CR
		\ compile filename -> filename.b
		dup ".b" + open-as-stdout \ write to filename.b
		dup compile-and-serialize \ compile filename -> filename.b
		void open-as-stdout \ restore stdout to write to the screen
		\ easiest thing to do now is load the serialized file i just created
		".b" + deserialize return
	;
	