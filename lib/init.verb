( -- default library, loaded at startup -- )
( -- Copyright (c) 2022 Frank McIngvale, see LICENSE -- )

(
	*** Read bootstrap/READ-ME-BEFORE-RUNNING.txt before editing this code! ***
	***       Changing this file can easily break the entire system!        ***
)

: . str puts 32 .c ;
: CR ( -- , prints CR LF ) 10 .c ;
: clear ( xn ... x1 -- ) SP depth + 1 - SP! ;

: drop ( a -- ) SP 1 + SP! ; 
: dup ( a -- a a ) SP ref ; 
: dup? ( a -- a a, only if a != 0 ) dup 0 == if >>ZERO dup @ZERO ; 
: swap ( a b -- b a ) over >L >L drop L> L> ;
: pick ( Sn .. S1 i -- Sn .. S1 Si ) SP + ref ; 

: over ( a b -- a b a ) SP 1 + ref ; 
: 2dup ( a b -- a b a b ) over over ; 
: 2drop ( a b -- ) SP 2 + SP! ; 
: 2over ( a b c d -- a b c d a b ) 4 pick 4 pick ; 
: 2swap ( a b c d -- c d a b ) 
    >L >L >L >L  
    LP 2 + ref LP 3 + ref LP ref LP 1 + ref LP 4 + LP! ; 

: ndrop ( xn .. x1 N -- ) SP + 1 + SP! ;
: nip ( a b -- b ) >L drop L> ;
: tuck ( a b -- b a b ) swap 2 pick ;
: rot ( a b c -- b c a ) 3 pick >L >L >L drop L> L> L> ;
: -rot ( b c a -- a b c ) 2 pick >L 3 pick >L >L drop drop L> L> L> ;

: not ( a -- not[a] ) if >>true true return @true false ;
: or ( a b -- a|b ) if >>true1 if >>true2 false return @true1 drop @true2 true ;
: and ( a b -- a&b ) if >>true1 drop false return @true1 if >>true2 false return @true2 true ;

: != ( a b -- a!=b ) == not ;
: >= ( a b -- a>=b ) 2dup > -rot == or ;
: < ( a b -- a<b ) swap > ;
: <= ( a b -- a<=b ) > not ;

: neg ( a -- -a ) dup int? if >>int 0 swap - return @int 0 swap - ;
: max ( a b -- max[a,b] ) 2dup > if >>A nip return @A drop ;
: min ( a b -- min[a,b] ) 2dup > if >>B drop return @B nip ;
: abs ( a -- abs[a] ) dup 0 < if >>neg return @neg neg ;
( 'floor' divide like in Python -- integers only )
: // ( a b -- quotient a/b ) /mod swap drop ;
: mod ( a b -- remainder a/b ) /mod drop ;

: :: make-closure ;

( .S - print stack without affecting it - good for debugging )
: .S "=>" . depth 
@LOOP 
	dup 0 > if >>true >>done @true dup SP + 1 + ref repr puts 32 .c 1 - <<LOOP @done drop CR ;

: .L ( N -- print first N locals .. meant for debugging, like .S )
	0 ( N index )
	"Locals:" .
	@loop
		( N index )
		2dup == if >>done
		dup str "L" swap + ":" + . dup LP + ref repr puts 32 .c
		1 + <<loop
	@done 2drop
	;

\ compiler turns "var NAME COUNT" into ( name count ,,var )
: ,,var ( name count -- ; creates new variable )
	alloc ( name addr )
	( make word that pushes addr onto stack )
	1 make-list ( name [ addr ] )
	swap make-word
	;
	
( shortcuts for addresses of first 10 locals )
: L0 LP ;
: L1 LP 1 + ;
: L2 LP 2 + ;
: L3 LP 3 + ;
: L4 LP 4 + ;
: L5 LP 5 + ;
: L6 LP 6 + ;
: L7 LP 7 + ;
: L8 LP 8 + ;
: L9 LP 9 + ;

: +get ( same as get but leaves obj on stack )
	over swap get ;

\ true|false if ANY member of sequence satisfies test function
: any? @( sequence test -- anytrue )
	@locals [ i ]
	0 i!
	@loop
		i sequence length >= if >>noneFound \ at end of sequence
		sequence i get test call \ pass i'th item to test
		if >>gotTrue \ found a true item, done
		i 1 + i! <<loop \ else ++i and continue
	@gotTrue
		true return
	@noneFound
		false return
	;

\ true|false if ALL members of sequence satisfy test
: all? @( sequence test -- alltrue )
	@locals [ i ]
	0 i!
	@loop
		i sequence length >= if >>allTrue \ end of sequence, didn't find any false
		sequence i get test call \ pass seq[i] to test
		if >>gotTrue 
		\ got one false item, so result is false
		false return
	@gotTrue
		i 1 + i! <<loop \ got true; ++i and continue
	@allTrue
		true return
	;

\ copy first nr locals into list [ LP LP+1 LP+2 ... ]
: copy-locals-to-list ( nr -- list )
	\ cannot use @( .. ) params since that would mess up the locals
	[ ]
	0
	@loop ( nr list i )
		dup SP 3 + ref ( nr list i i nr ) >= if >>done
		dup LP + ref ( nr list i Li )
		SP 2 + ref swap ( nr list i list Li ) append drop ( nr list i )
		1 + ( nr list i+1 ) <<loop
	@done ( nr list i )
		drop nip
	;	
	
\ ========================================================================
\ String functions
\ ========================================================================


: whitespace? ( string -- bool; is first char of string whitespace? )
	0 get ord ( get as ascii )
	32 <= ( treat everything <= space as whitespace )
	;

\ return string with any leading whitespace removed
: string-ltrim ( string -- string )
	0
	@loop ( string i )
		2dup swap length >= ( string i i>=len ) if >>empty
		2dup get whitespace? not ( string i !ws?[string[i]] ) if >>endtrim 
		1 + <<loop
	@endtrim ( string i )
		-1 slice return
	@empty ( string i )
		drop drop "" return
	;

\ is string zero length or contain only whitespace?
: string-empty? ( string -- empty? )
	string-ltrim length 0 ==
	;

\ join a list of strings by separator string
: string-join ( list separator -- string )
	@args [ list sep ]
	list
	@{ @args [ accum elem ]
		accum length 0 == if >>skip \ skip separator before first string
		accum sep + elem + return
		@skip elem return
	} "" fold
	;
	
: string->symbol ( string -- symbol )
	unmake make-symbol
	;

: symbol->string ( symbol -- string )
	unmake make-string
	;

: ord ( string -- ascii ; get ASCII value of first char of string )
	0 get unmake drop
	;

: chr ( ASCII -- string ; turn ASCII character value into single-character string )
	1 make-string
	;

\ is c an uppercase letter?
: upper? @( c ) 
	c ord 65 >= c ord 90 <= and ;
	
\ is c an uppercase letter?
: lower? @( c )
	c ord 97 >= c ord 122 <= and ;
	
\ convert char c to lowercase, if it is a letter
: tolower @( c -- lowercase )
	c upper? if >>is-upper
	c return \ not uppercase, return c as is
	@is-upper
		c ord 65 - 97 + chr
	;

\ convert char c to uppercase, if it is a letter
: toupper @( c -- uppercase )
	c lower? if >>is-lower
	c return \ not uppercase, return call
	@is-lower
		c ord 97 - 65 + chr
	;
	
\ convert string to lowercase
: string-lower @( s -- lowercase )
	s { tolower } map
	;

\ convert string to uppercase	
: string-upper @( s -- lowercase )
	s { toupper } map
	;
	
\ split text into parts separated by one or more chars from delims (a string or list of chars).
\ any leading/trailing delims are trimmed. this function does NOT make empty parts.
: string-split @( text delims -- parts )
	@locals [ i c piece ]
	[ ] parts!
	0 i!
	"" piece!
	@loop
		text i get c!
		c void? if >>got-end \ end of string
		delims @{ c == } find void? not if >>got-delim \ found delimiter
		\ not a delim, add to piece
		piece c + piece! >>next
	@got-delim
		\ if i've been building a piece, add to parts and reset piece
		piece length 0 > if >>push-piece
		\ else this in the middle of other delimiters, so just keep going
		>>next
	@push-piece
		parts piece append parts!
		"" piece!
		>>next
	@next
		i 1 + i! <<loop
	@got-end
		\ if i have a piece, add it to parts
		piece length 0 == if >>done 
		parts piece append parts!
	@done
		parts
	;
	
\ ========================================================================
\ Branching
\ ========================================================================

\ calls if-true if cond is true else calls if-false
\ (cond must be a bool)
: ifelse @( cond if-true if-false )
	cond if >>true
	if-false call return
	@true if-true call
	;

\ calls if-true if cond is true, else does nothing
: ifthen @( cond if-true )
	cond if >>true
	return \ nothing to do on false
	@true
		if-true call
	;

\ ========================================================================
\ Iteration/looping/functional programming
\ ========================================================================

\ call func NR times -- does not touch stack; func can use stack in any way
: repeat @( func NR -- )
	\ lambda is allowed to use stack, but not locals, so store my
	\ parameters in locals
	@loop
		NR 0 <= if >>done 
		func call \ call func - stack is in same state as last call to fund
		NR 1 - NR! <<loop
	@done
		;

\	for-each -- call a lambda on each element of a sequence [any object that supports
\  	'length' and 'slice' methods]
\
\	lambda is called with current item of sequence on top of stack. for-each does not
\	touch the stack otherwise.
\
\	*NOTE* lambda CANNOT rely on the state of the stack or locals -- bind with state if 
\	data is needed other than the passed elements
\
: for-each @( seq func -- )
	@locals [ i ]
	0 i!
	@loop
		i seq length >= if >>done 
		seq i get func call
		i 1 + i! <<loop
	@done 
		;
	

\
\	for-each-reverse -- same as for-each, but starts at the end
\
: for-each-reverse @( seq func -- )
	@locals [ i ]
	seq length 1 - i!
	@loop
		i 0 < if >>done 
		seq i get func call 
		i 1 - i! <<loop
	@done 
		;

\
\ like for-each, except includes index. lambda is called as:
\	: lambda ( i elem -- ) 
\
\ *NOTE* lambda CANNOT rely on the state of the stack or locals -- bind with state if 
\ data is needed other than the passed ( i elem )
\
: for-each-i @( seq func -- )
	@locals [ i ]
	0 i!
	@loop
		i seq length >= if >>done 
		i seq i get func call 
		i 1 + i! <<loop
	@done 
		;
	
\ variant of for-each that calls func with ( key val )
: for-each-dict @( dict func -- )
	@locals [ i keylist ]
	dict keys keylist!
	0 i!
	@loop
		i keylist length >= if >>done 
		keylist i get dup dict swap get func call
		i 1 + i! <<loop
	@done 
		;
		
\ for i=start .. start+incr*(count-1), call fn with args ( i )
\
\ note: count is used instead of an end value to simplify boundary conditions
: for-range @( fn start incr count )
	@locals [ i ]
	
	0 i!
	@loop
		count 0 <= if >>done
		\ call fn with start+i on top of stack
		i start + fn call
		i incr + i!
		count 1 - count!
		<<loop
	@done
	;
	
\ find index of first item in sequence that satisfies test or void if none 
: find @( seq test -- index )
	0 index!
	@loop
		index seq length >= if >>not-found
		seq index get test call if >>found 
		index 1 + index! <<loop
	@found index return
	@not-found void
	;

\ fold - left fold
\	func: called as: ( accum elem -- new-accum ) where accum is the accumulated value and
\		  elem is the next element to be added to the accumulator.
\		  func must leave updated accum on stack
\	init: initial value for accumulator
\
\ 	pseudocode:
\		acc = func(acc, seq[0])
\		acc = func(acc, seq[1])
\		.
\		acc = func(acc, seq[n-1])
: fold @( seq func init -- accum )
	@locals [ i ]
	init accum!
	0 i!
	@loop
		i seq length >= if >>done 
		accum seq i get func call accum!
		i 1 + i! <<loop
	@done
		accum
	;

\ call func for each item of list as:
\	( list[i] -- out )
\ make a newlist from the 'out' values
: map-list @( list func -- newlist )
	@locals [ i ]
	0 i!
	[ ] newlist!
	@loop
		i list length >= if >>done 
		list i get func call
		newlist swap append newlist!
		i 1 + i! <<loop
	@done
		newlist
		;

\ call func for each char of string as:
\	( str[i] -- out )
\ make a newlist from the 'out' values
: map-string @( str func -- newstr )
	@locals [ i ]
	0 i!
	"" newstr!
	@loop
		i str length >= if >>done 
		str i get func call
		newstr swap + newstr!
		i 1 + i! <<loop
	@done
		newstr
		;

\
\ map - make new sequence of func[elem] for each elem in sequence
\
\ calls func as:
\	: func ( elem -- new-elem )
\
: map @( seq func -- newseq )
	seq list? if >>list 
	seq string? if >>string 
	"Bad sequence in map:" seq str + error

	@list seq func map-list return
	@string seq func map-string return
	;

: filter-list @( list func -- truelist )
	@locals [ i ]
	[ ] truelist!
	0 i!
	@loop
		i list length >= if >>done 
		list i get func call not if >>next 
		truelist list i get append truelist!
	@next
		i 1 + i! <<loop
	@done
		truelist
	;

: filter-string @( str func -- truestring )
	@locals [ i ]
	"" truestring!
	0 i!
	@loop
		i str length >= if >>done 
		str i get func call not if >>next 
		truestring str i get + truestring!
	@next
		i 1 + i! <<loop
	@done
		truestring
	;

\
\ filter: make new sequence of only elements where func[elem] is true
\
\ calls func with elem on top of stack; func must leave true or false on stack
\
: filter ( sequence func -- new-sequence )
	over list? if >>list 
	over string? if >>string
	drop "Bad sequence in filter: " swap str + error

	@list filter-list return 
	@string filter-string return
	;
	
\
\ take a list of tuples and makes a dictionary:
\	[ [ "foo" 123 ] [ "bar" 678 ] ] make-dict
\
: make-dict ( list-of-pairs -- dict )
	\ called as: ( dict [ key val ] )
	{ unmake drop put } ,,new-dict fold
	;

\ ( message -- ) ;  print a debug message along with the stack
: debug . .S CR ;

\ -------------------------------------------------------------------------
\
\ Stream reader
\
\ -------------------------------------------------------------------------

\ create a new stream for reading from string
: stream-reader-open-string ( string -- stream )
	\ state = [ TEXT POSITION ]
	0 2 make-list
	;

\ create a new stream for reading from file
: stream-reader-open-file ( filename -- stream )
	file-read stream-reader-open-string
	;

\ since doing a series of reads is such a common operation,
\ these functions leave the stream on the stack to avoid lots of caller dups
\ and also usually avoids drops here

\ peek next char [as string] from stream or void on eof
: stream-reader-peek-char ( stream -- stream c )
	\ POS >= length?
	dup 1 get over 0 get length >= if >>eof
	\ POS is OK
	dup 0 get over 1 get get return
	
	@eof void
	;

\ read next char [as string] from stream or void on eof
: stream-reader-next-char ( stream -- stream c )
	\ POS >= length?
	dup 1 get over 0 get length >= if >>eof
	\ POS is OK, get char
	dup 0 get over 1 get get ( stream c )
	\ ++POS
	swap dup 1 get 1 + 1 swap put ( c stream )
	swap return 

	@eof void
	;

\ skip all whitespace from current position
: stream-reader-skip-whitespace ( stream -- stream )
	@loop
		stream-reader-peek-char
		dup void? if >>done
		dup whitespace? not if >>done
		drop 
		stream-reader-next-char drop <<loop
	@done
		drop
		;

\ read word (non-whitespace chunk) from current position,
\ skipping any leading whitespace. returns void if no more words.
: stream-reader-next-word ( stream -- stream word )
	stream-reader-skip-whitespace
	"" ( stream word )
	@loop ( stream word )
		over stream-reader-next-char nip ( stream word c )
		dup void? if >>done
		dup whitespace? if >>done
		+ <<loop \ non-ws, append and continue
	@done ( stream word c )
		drop
		dup length 0 == if >>void
		return
	@void
		drop void
	;

\ get next line from reader or void
\ discards any \r or \n line ending so an empty line will return ""
\ returns void if no more lines
: stream-reader-next-line ( stream -- stream line )
	""
	@loop ( stream s )
		\ "@loop:" . .S CR
		over stream-reader-next-char nip ( stream s c )
		dup void? if >>eof
		dup ord 10 == if >>NL \ \n
		dup ord 13 == if >>CR \ \r
		+ <<loop \ normal char - append and contine
	\ this is gross but i want to be portable so check for
	\ any of: \n, \r, \r\n, \n\r
	@NL ( stream s \n )
		drop over stream-reader-peek-char nip ( stream s c )
		dup void? if >>drop-return
		dup ord 13 == if >>skip-next
		>>drop-return
	@CR ( stream s \r )
		drop over stream-reader-peek-char nip ( stream s c )
		dup void? if >>drop-return
		dup ord 10 == if >>skip-next
		>>drop-return
	@drop-return
		\ just \r or \n
		drop return
	@skip-next ( stream s c )
		\ got CR/LF pair
		\ skip next char
		drop over stream-reader-next-char drop drop return
	@eof ( stream s c )
		drop 
		\ need to distinguish eof at end of string vs. eof at start
		dup length 0 == if >>void \ no chars before eof
		return
	@void ( stream s )
		drop void
		;
		
: stream-reader-peek-line @( stream -- stream' line )
	@locals [ savepos ]
	stream 1 get savepos!
	stream stream-reader-next-line line!
	\ reset stream back to start of line
	1 savepos put line
	;
